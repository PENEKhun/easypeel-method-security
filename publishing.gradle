apply plugin: 'java-library'
pluginManager.apply("com.vanniktech.maven.publish")

mavenPublishing {
    coordinates(rootPackageName, rootProject.name, projectVersion)

    var requireEnv = [
            'ORG_GRADLE_PROJECT_mavenCentralUsername',
            'ORG_GRADLE_PROJECT_mavenCentralPassword',
            'ORG_GRADLE_PROJECT_signingInMemoryKey',
            'ORG_GRADLE_PROJECT_signingInMemoryKeyId'
    ]

    boolean withSign = true
    for (env in requireEnv) {
        if (System.getenv(env) == null) {
            println "[Warning] Deploying without running signing because there is no environment variable about ${env}. This message must only be output on Maven Local deployments."
            withSign = false
            break;
        }
    }

    if (withSign) {
        signAllPublications()
    }

    pom {
        name = projectName
        description = 'Configure method-level security easily in your RestController'
        url = 'https://github.com/easypeel-security/easypeel-method-security/'

        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id = 'PENEKhun'
                name = 'MoonSeonghun'
                email = 'penekhun@gmail.com'
            }
        }

        scm {
            connection = 'scm:git:git://github.com/easypeel-security/easypeel-method-security.git'
            developerConnection = 'scm:git:ssh://github.com/easypeel-security/easypeel-method-security'
            url = 'https://github.com/easypeel-security/easypeel-method-security.git'
        }
    }
}
