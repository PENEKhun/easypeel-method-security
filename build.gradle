plugins {
	id 'java-library'
	id "com.vanniktech.maven.publish" version "0.27.0"
	id 'io.github.goooler.shadow' version '8.1.3'
//	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

apply from: 'lint.gradle'

java {
	withJavadocJar()
	withSourcesJar()
}

subprojects {
	group = rootPackageName
	version = easypeelMethodSecurityVersion

	repositories {
		mavenCentral()
	}

	apply plugin: 'java'
	apply plugin: 'jacoco'

	dependencies {
		testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
		testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
		testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
		testImplementation "org.assertj:assertj-core:${assertjVersion}"
		testImplementation "com.google.testing.compile:compile-testing:${compileTestingVersion}"
		testImplementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
		testImplementation "org.springframework.boot:spring-boot-starter-aop:${springBootVersion}"
	}

	test {
		useJUnitPlatform()
		finalizedBy jacocoTestReport
	}

	jacocoTestReport {
		dependsOn test // tests are required to run before generating the report
		reports {
			xml.required.set(true)
		}
	}

	shadowJar {
		archiveBaseName.set(easypeelMethodSecurity)
		archiveVersion.set(easypeelMethodSecurityVersion)
		archiveClassifier.set("")
		archiveExtension.set("jar")

		relocate("com.squareup.javapoet", "org.epsec.javapoet")
		relocate("com.github.benmanes.caffeine", "org.epsec.caffeine")
		relocate("org.aspectj", "org.epsec.aspectj")
	}

}
